buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}

plugins {
	id "com.jfrog.bintray" version "1.4"
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

group 'org.squiddev'
version '0.1.0'

sourceCompatibility = 1.8

minecraft {
	version = "1.12-14.21.1.2387"
	mappings = "snapshot_20170723"
}

repositories {
	mavenCentral()
}

dependencies {
	testCompile group: 'junit', name: 'junit', version: '4.12'

	compileOnly "com.google.auto.service:auto-service:1.0-rc3"
	compile files("${System.properties['java.home']}/../lib/tools.jar")
}

compileTestJava {
	options.compilerArgs += ['-processorpath', sourceSets.main.output.classesDir.toString(), "-Xplugin:ForgeLint"]
	println options.compilerArgs
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['mavenJava']
	publish = true
	pkg {
		repo = 'maven'
		name = 'ForgeLint'
		licenses = ['BSD-3']
		vcsUrl = 'https://github.com/SquidDev/ForgeLint'

		version {
			name = project.version
			desc = 'Static analysis for Forge mods'
			released = new Date()
		}
	}
}
